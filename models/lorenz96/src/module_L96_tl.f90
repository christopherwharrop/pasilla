module L96_TL

  use kind,           only : r8kind
  use Abstract_Model, only : abstract_model_type
  use L96_Config,     only : l96_config_type

  implicit none

  private

  public :: l96_tl_type

  type, extends(abstract_model_type) :: l96_tl_type
    private
    type(l96_config_type)     :: config
    integer                   :: step
    real(r8kind)              :: clock
    real(r8kind), allocatable :: state(:)
    real(r8kind), allocatable :: location(:)
    real(r8kind), allocatable :: trajectory(:)
  contains
    final              :: destructor_tl
    procedure, private :: comp_dt
    procedure, private :: comp_dt_d
    procedure          :: adv_nsteps
    procedure          :: get_config
    procedure          :: get_location
    procedure          :: get_state
    procedure          :: get_trajectory
    procedure          :: get_step
    procedure          :: get_clock
    procedure          :: print
    procedure          :: interpolate
  end type l96_tl_type

  interface l96_tl_type
     procedure constructor_tl
  end interface


contains

  !------------------------------------------------------------------
  ! constructor_tl
  !
  ! Returns an initialized l96_tl_type object
  !------------------------------------------------------------------
  type(l96_tl_type) function constructor_tl(config, state, trajectory, step)

    class(l96_config_type), intent(in) :: config
    real(r8kind), optional, intent(in) :: state(:)
    real(r8kind), optional, intent(in) :: trajectory(:)
    integer,      optional, intent(in) :: step

    integer :: j

    constructor_tl%config = config

    ! Initialize model step
    if (present(step)) then
      constructor_tl%step = step
    else
      constructor_tl%step = 0
    end if

    ! Initialize model clock
    constructor_tl%clock = constructor_tl%step * config%get_time_step()

    ! Localize the model domain
    allocate(constructor_tl%location(config%get_nx()))
    do j = 1, config%get_nx()
      constructor_tl%location(j) = (j - 1.0_r8kind) / config%get_nx()
    end do

    ! Initialize model state
    allocate(constructor_tl%state(config%get_nx()))
    if (present(state)) then
      constructor_tl%state(:) = state(:)
    else
      constructor_tl%state(:) = config%get_forcing()
      constructor_tl%state(1) = 1.001_r8kind * config%get_forcing()
    end if

    ! Initialize model trajectory
    allocate(constructor_tl%trajectory(config%get_nx()))
    if (present(trajectory)) then
      constructor_tl%trajectory(:) = trajectory(:)
    else
      constructor_tl%trajectory(:) = constructor_tl%state(:)
    end if

  end function constructor_tl


  !------------------------------------------------------------------
  ! destructor_tl
  !
  ! Deallocates pointers used by a l96_tl_type object (none currently)
  !------------------------------------------------------------------
  elemental subroutine destructor_tl(this)

    type(l96_tl_type), intent(inout) :: this

    ! No pointers in l96_tl_type object so we do nothing

  end subroutine destructor_tl


  !------------------------------------------------------------------
  !        Generated by TAPENADE     (INRIA, Ecuador team)
  !  Tapenade 3.11 (r6148) - 16 Aug 2016 14:18
  !
  !  Differentiation of comp_dt in forward (tangent) mode:
  !   variations   of useful results: dt
  !   with respect to varying inputs: dt x
  !------------------------------------------------------------------
  ! comp_dt_d
  !
  ! Private routine to compute the time tendency of a l96_tl_type object
  ! given a state, x, and return it in dt.
  !------------------------------------------------------------------
  subroutine comp_dt(this, state, stated, dt, dtd)

    class(l96_tl_type), intent( in) :: this
    real(r8kind),       intent( in) :: state(:)
    real(r8kind),       intent( in) :: stated(:)
    real(r8kind),       intent(out) :: dt(size(state))
    real(r8kind),       intent(out) :: dtd(size(stated))

    integer :: nelements
    integer :: j

    ! Get the size of the array
    nelements = size(state)

    ! compute dtd(1)
    dtd(1) = (stated(2) - stated(nelements - 1)) * state(nelements) + (state(2) - state(nelements - 1)) * stated(nelements) - stated(1)
    dt(1) =  ( state(2) -  state(nelements - 1)) * state(nelements) - state(1) + this%config%get_forcing()

    ! compute dtd(2)
    dtd(2) = (stated(3) - stated(nelements)) * state(1) + (state(3) - state(nelements)) * stated(1) - stated(2)
    dt(2) =  (state(3) -   state(nelements)) * state(1) - state(2) + this%config%get_forcing()

    ! compute dtd(3) thru dtd(size -1)
    do j = 3, nelements - 1
       dtd(j) = (stated(j + 1) - stated(j - 2)) * state(j - 1) + (state(j + 1) - state(j - 2)) * stated(j - 1) - stated(j)
        dt(j) = ( state(j + 1) -  state(j - 2)) * state(j - 1) - state(j) + this%config%get_forcing()
    end do

    ! compute dtd(size)
    dtd(nelements) = (stated(1) - stated(nelements - 2)) * state(nelements - 1) + (state(1) - state(nelements - 2)) * stated(nelements - 1) - stated(nelements)
    dt(nelements) =  ( state(1) -  state(nelements - 2)) * state(nelements - 1) - state(nelements) + this%config%get_forcing()

  end subroutine comp_dt

  function comp_dt_d(this, state, stated, comp_dt)

    class(l96_tl_type), intent(inout) :: this
    real(r8kind), intent(in) :: state(:)
    real(r8kind), intent(in) :: stated(:)
    real(r8kind), dimension(size(state)) :: comp_dt
    real(r8kind), dimension(size(state)) :: comp_dt_d
    integer :: nelements
    integer :: j
    real(r8kind) :: forcing

    ! Get the forcing from the configuration
    forcing = this%config%get_forcing()

    ! Get the size of the array
    nelements = size(state)

    ! compute comp_dt(1)
    comp_dt_d = 0.0_8
    comp_dt_d(1) = (stated(2) - stated(nelements - 1)) * state(nelements) + (state(2) - state(nelements - 1)) * stated(nelements) - stated(1)
    comp_dt(1) = (state(2) - state(nelements - 1)) * state(nelements) - state(1) + forcing

    ! compute comp_dt(2)
    comp_dt_d(2) = (stated(3) - stated(nelements)) * state(1) + (state(3) - state(nelements)) * stated(1) - stated(2)
    comp_dt(2) = (state(3) - state(nelements)) * state(1) - state(2) + forcing

    ! compute comp_dt(3) thru comp_dt(size -1)
    do j = 3, nelements - 1
      comp_dt_d(j) = (stated(j + 1) - stated(j - 2)) * state(j - 1) + (state(j + 1)- state(j - 2)) * stated(j - 1) - stated(j)
      comp_dt(j) = (state(j + 1) - state(j - 2)) * state(j - 1) - state(j) + forcing
    end do

    ! compute comp_dt(size)
    comp_dt_d(nelements) = (stated(1) - stated(nelements - 2)) * state(nelements - 1) + (state(1) - state(nelements - 2)) * stated(nelements - 1) - stated(nelements)
    comp_dt(nelements) = (state(1) - state(nelements - 2)) * state(nelements - 1) - state(nelements) + forcing

  end function comp_dt_d


  !------------------------------------------------------------------
  !        Generated by TAPENADE     (INRIA, Ecuador team)
  !  Tapenade 3.11 (r6148) - 16 Aug 2016 14:18
  !
  !  Differentiation of adv_nsteps in forward (tangent) mode:
  !   variations   of useful results: state
  !   with respect to varying inputs: state
  !   RW status of diff variables: state:in-out
  !------------------------------------------------------------------
  ! adv_nsteps_d
  !
  ! Does n time step advances for lorenz 96 model
  ! using four-step rk time step
  !------------------------------------------------------------------
  subroutine adv_nsteps(this, nsteps)

    class(l96_tl_type), intent(inout) :: this
    integer, intent(in) :: nsteps

    real(r8kind), dimension(this%config%get_nx()) :: x1, x2, x3, x4, dx, inter
    real(r8kind), dimension(this%config%get_nx()) :: x1d, x2d, x3d, x4d, interd
    real(r8kind), dimension(this%config%get_nx()) :: result1
    real(r8kind), dimension(this%config%get_nx()) :: result1d
    integer :: step

    real(r8kind), dimension(this%config%get_nx(),this%config%get_nx()) :: mprime
    real(r8kind) :: time_step

    time_step = this%config%get_time_step()

    do step = 1, nsteps

      !  Compute the first intermediate step
      result1d = this%comp_dt_d(this%state, this%trajectory, result1)
      x1d = time_step*result1d
      x1 = time_step*result1
      interd = this%trajectory + x1d/2.0_r8kind
      inter = this%state + x1/2.0_r8kind

      !  Compute the second intermediate step
      result1d = this%comp_dt_d(inter, interd, result1)
      x2d = time_step*result1d
      x2 = time_step*result1
      interd = this%trajectory + x2d/2.0_r8kind
      inter = this%state + x2/2.0_r8kind

      !  Compute the third intermediate step
      result1d = this%comp_dt_d(inter, interd, result1)
      x3d = time_step*result1d
      x3 = time_step*result1
      interd = this%trajectory + x3d
      inter = this%state + x3

      !  Compute fourth intermediate step
      result1d = this%comp_dt_d(inter, interd, result1)
      x4d = time_step*result1d
      x4 = time_step*result1

      ! Update state with original trajectory
      this%state = this%state + this%trajectory

      !  Update trajectory
      this%trajectory = this%trajectory + x1d/6.0_r8kind + x2d/3.0_r8kind + x3d/3.0_r8kind + x4d/6.0_r8kind


! Lidia's matrix formulation with correct trajectory
! NOTE: The matrix forumlation is much less accurate because it does not represent the derivative of the
!       calculations performed by the time differencing and rk4 algorithms.
!44    FORMAT(A8,6F10.6)
!      call buildMprime(this%state, mprime)
!      this%state = this%state + this%trajectory
!      this%trajectory = this%trajectory + this%config%get_time_step() * matmul(mprime, this%trajectory)

      ! Increment time step
      this%clock = this%clock + this%config%get_time_step()
      this%step = this%step + 1

    end do

  end subroutine adv_nsteps


  !------------------------------------------------------------------
  ! buildMprime
  !
  ! Constructs matrix for computing lorenz96 derivatives.
  ! Used to advance the TL and ADJ models.
  !------------------------------------------------------------------
  subroutine buildMprime(x,mm)

    real(r8kind) :: x(:)
    real(r8kind) :: mm(:,:)

    real(r8kind), allocatable :: tmp(:)

    integer :: n,i

    n=size(x)
    allocate(tmp(n))
    mm(:,:)=0.0
    do i=1,n
      tmp = 0.0
      tmp(1) = -1.0
      tmp(2) = x(mmod(i-1, n))
      tmp(n-1) = -x(mmod(i-1,n))
      tmp(n) = x(mmod(i+1,n))-x(mmod(i-2,n))
      tmp =cshift(tmp,-1*(i-1))
      mm(i,:) = tmp
    end do

  end subroutine buildMprime


  !------------------------------------------------------------------
  ! mmod
  !
  ! Helper function used by buildMprime to compute modified mod function
  !------------------------------------------------------------------
  integer function mmod(x,n)

    integer :: x,n

    mmod=modulo(x,n)
    if (mmod==0) mmod=n

  end function mmod


  !------------------------------------------------------------------  
  ! get_config
  !
  ! Return the model configuration
  !------------------------------------------------------------------  
  pure type(l96_config_type) function get_config(this)

    class(l96_tl_type), intent(in) :: this

    get_config = this%config

  end function get_config


  !------------------------------------------------------------------  
  ! get_location
  !
  ! Return the model location vector
  !------------------------------------------------------------------  
  pure function get_location(this)

    class(l96_tl_type), intent(in) :: this
    real(r8kind), dimension(this%config%get_nx()) :: get_location

    get_location = this%location

  end function get_location


  !------------------------------------------------------------------  
  ! get_state
  !
  ! Return the model state vector
  !------------------------------------------------------------------  
  pure function get_state(this)

    class(l96_tl_type), intent(in) :: this
    real(r8kind), dimension(this%config%get_nx()) :: get_state

    get_state = this%state

  end function get_state


  !------------------------------------------------------------------
  ! get_trajectory
  !
  ! Return the model trajectory vector
  !------------------------------------------------------------------
  pure function get_trajectory(this)

    class(l96_tl_type), intent(in) :: this
    real(r8kind), dimension(this%config%get_nx()) :: get_trajectory

    get_trajectory = this%trajectory

  end function get_trajectory


  !------------------------------------------------------------------
  ! get_step
  !
  ! Return the model step
  !------------------------------------------------------------------  
  pure integer function get_step(this)

    class(l96_tl_type), intent(in) :: this

    get_step = this%step

  end function get_step



  !------------------------------------------------------------------  
  ! get_clock
  !
  ! Return the model clock
  !------------------------------------------------------------------  
  pure real(r8kind) function get_clock(this)

    class(l96_tl_type), intent(in) :: this

    get_clock = this%clock

  end function get_clock


  !------------------------------------------------------------------  
  ! print
  !
  ! Print the contents of this model object
  !------------------------------------------------------------------  
  subroutine print(this)

    class(l96_tl_type), intent(in) :: this

    character(len=32) :: numstr, varstr, indexstr
    integer :: i

    call this%config%print()

    write(numstr,'(I)') this%step
    write(*,'(A20,A)') 'step = ', adjustl(numstr)
    write(numstr,'(F16.13)') this%clock
    write(*,'(A20,A)') 'clock = ', adjustl(numstr)
    do i=1, this%config%get_nx()
      write(numstr,'(F16.13)') this%state(i)
      write(indexstr,'(I)') i
      write(*,'(A20,A)') 'state(' // trim(adjustl(indexstr)) // ') = ', numstr
    end do

  end subroutine print


  !------------------------------------------------------------------  
  ! Interpolates from state vector state to the location. 
  !------------------------------------------------------------------  
  subroutine interpolate(this, location, state_val)

    class(l96_tl_type), intent(in) :: this
    real(r8kind), intent(in)    :: location
    real(r8kind), intent(out)   :: state_val

    integer :: lower_index, upper_index, i, size
    real(r8kind) :: lctn, lctnfrac

    ! Get the number of grid points
    size = this%config%get_nx()

    ! Scale the location to the size of the domain
    lctn = size * location

    ! Compute grid indices bounding the location
    lower_index = int(lctn) + 1
    upper_index = lower_index + 1
    if(lower_index > size) lower_index = lower_index - size
    if(upper_index > size) upper_index = upper_index - size

    ! Interpolate model value at the location
    lctnfrac = lctn - int(lctn)
    state_val = (1.0_r8kind - lctnfrac) * this%state(lower_index) + lctnfrac * this%state(upper_index)

  end subroutine interpolate


end module L96_TL
